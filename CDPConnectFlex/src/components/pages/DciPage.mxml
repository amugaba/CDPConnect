<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009"
					xmlns:s="library://ns.adobe.com/flex/spark"
					xmlns:mx="library://ns.adobe.com/flex/mx"
					xmlns:custom="components.*"
					xmlns:gpraservice="services.gpraservice.*"
					xmlns:styles="com.adobe.fiber.styles.*"
					xmlns:clientservice="services.clientservice.*"
					xmlns:dci="components.dci.*"
					xmlns:assessmentservice="services.assessmentservice.*"
					width="400" height="300">
	<fx:Script>
		<![CDATA[
			import com.adobe.serializers.utility.TypeUtility;
			
			import components.assessment.AssessType;
			import components.dci.DCIForm;
			import components.dci.InterviewType;
			import components.questions.QuestionClass;
			import components.questions.QuestionList;
			import components.questions.QuestionText;
			import components.questions.QuestionTextRefusable;
			import components.skips.SkipPattern;
			import components.validators.DateAgeValidator;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.DateField;
			import mx.controls.tabBarClasses.Tab;
			import mx.core.FlexGlobals;
			import mx.events.ValidationResultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.validators.ValidationResult;
			
			import valueObjects.AssessmentVO;
			import valueObjects.ClientVO;
			import valueObjects.GpraVO;
			
			protected var questionDict:Dictionary = new Dictionary();
			
			protected var global:CDPConnectFlex = FlexGlobals.topLevelApplication as CDPConnectFlex;
			protected var dciForms:Array = new Array();
			protected var dciTabs:Array = new Array();
			protected var tabButtons:Array = new Array();
			protected var activeTabIndexes:Array = new Array();
			protected var activeFormIndexes:Array = new Array();
			protected var created:Boolean = false;
			public var interviewType:int;
			
			public function initializePage(type:int):void
			{				
				interviewType = type;
				//viewStack.selectedChild = recordTab;
				
				if(!created)
				{
					dciForms = new Array(recordForm,sbirtForm,services1Form,services2Form,demoForm,demo2Form,military1Form,military2Form,drugForm,drug2Form,livingForm,employmentForm,
						crimeForm,health1Form,health2Form,health3Form,health4Form,health5Form,socialForm,followupForm,dischargeForm,dischargeServices1Form,dischargeServices2Form);
					dciTabs = new Array(recordTab,sbirtTab,services1Tab,services2Tab,demoTab,demo2Tab,military1Tab,military2Tab,drugTab,drug2Tab,livingTab,employmentTab,
						crimeTab,health1Tab,health2Tab,health3Tab,health4Tab,health5Tab,socialTab,followupTab,dischargeTab,dischargeServices1Tab,dischargeServices2Tab,saveTab);

					for each(var btmp:Tab in tabBar.getChildren())
						tabButtons.push(btmp);
					created = true;
				}
				
				recordForm.initForm();
				
				if(type == InterviewType.INTAKE_SBIRT_NEG)
				{
					enableSectionSBIRT(true);
					enableSectionA(true);
					enableSectionB(false);
					enableSectionCtoG(false);
					enableSectionI(false);
					enableSectionJ(false);
					enableSectionK(false);
				}
				else if(type == InterviewType.INTAKE_SBIRT_BI)
				{
					enableSectionSBIRT(true);
					enableSectionA(true);
					enableSectionB(true);
					enableSectionCtoG(false);
					enableSectionI(false);
					enableSectionJ(false);
					enableSectionK(false);
				}
				else if(type == InterviewType.INTAKE_SBIRT_RT)
				{
					enableSectionSBIRT(true);
					enableSectionA(true);
					enableSectionB(true);
					enableSectionCtoG(true);
					enableSectionI(false);
					enableSectionJ(false);
					enableSectionK(false);
				}
				else if(type == InterviewType.INTAKE_GENERAL)
				{
					enableSectionSBIRT(false);
					enableSectionA(true);
					enableSectionB(true);
					enableSectionCtoG(true);
					enableSectionI(false);
					enableSectionJ(false);
					enableSectionK(false);
				}
				else if(type == InterviewType.FOLLOWUP)
				{
					enableSectionSBIRT(false);
					enableSectionA(false);
					enableSectionB(true);
					enableSectionCtoG(true);
					enableSectionI(true);
					enableSectionJ(false);
					enableSectionK(true);
				}
				else if(type == InterviewType.FOLLOWUP_SBIRT_BI)
				{
					enableSectionSBIRT(false);
					enableSectionA(false);
					enableSectionB(true);
					enableSectionCtoG(false);
					enableSectionI(true);
					enableSectionJ(false);
					enableSectionK(true);
				}
				else if(type == InterviewType.DISCHARGE)
				{
					enableSectionSBIRT(false);
					enableSectionA(false);
					enableSectionB(true);
					enableSectionCtoG(true);
					enableSectionI(false);
					enableSectionJ(true);
					enableSectionK(true);
				}
				else if(type == InterviewType.DISCHARGE_NO_INTERVIEW)
				{
					enableSectionSBIRT(false);
					enableSectionA(false);
					enableSectionB(false);
					enableSectionCtoG(false);
					enableSectionI(false);
					enableSectionJ(true);
					enableSectionK(true);
				}
				
				//Determine which items are active
				activeTabIndexes = new Array();
				activeFormIndexes = new Array();
				for(var i:int = 0; i < tabButtons.length; i++)
				{
					if(tabButtons[i].visible)
					{
						activeTabIndexes.push(i);
						if(i < dciForms.length)
							activeFormIndexes.push(i);
					}
				}
				
				//If loading a DCI
				if(global.activeAssessment != null)
				{
					//Load data
					for(i = 0; i < dciForms.length; i++)
					{
						//if this form is active
						if(viewStack.contains(dciTabs[i]))
							(dciForms[i] as DCIForm).loadData(global.activeAssessment.data.source);
					}
					
					//Then validate
					for(i = 0; i < dciForms.length; i++)
					{
						//if this form is active
						if(viewStack.contains(dciTabs[i]))
							(dciForms[i] as DCIForm).validateForm();
					}
					
					//Then process skips
					for(i = 0; i < dciForms.length; i++)
					{
						//if this form is active
						if(viewStack.contains(dciTabs[i]))
							(dciForms[i] as DCIForm).doAllSkips();
					}
				}
				
				//Reorder all tab buttons so that the invisible ones are at the bottom
				//Have to reorder the tabs in the viewstack to correspond to the new order too though
				var index:int = 0;
				for each(i in activeTabIndexes)
				{
					viewStack.setChildIndex(dciTabs[i],index);
					index++;
				}
			}
			
			protected function enableTab(tab:NavigatorContent, enabled:Boolean):void
			{
				(tabButtons[dciTabs.indexOf(tab)] as Tab).visible = enabled;
			}
			
			protected function enableSectionSBIRT(enabled:Boolean):void
			{
				sbirtForm.initForm();
				enableTab(sbirtTab,enabled);
			}
			
			protected function enableSectionB(enabled:Boolean):void
			{
				drugForm.initForm();
				drug2Form.initForm();
				enableTab(drugTab,enabled);
				enableTab(drug2Tab,enabled);
			}
			
			protected function enableSectionCtoG(enabled:Boolean):void
			{
				livingForm.initForm();
				employmentForm.initForm();
				crimeForm.initForm();
				health1Form.initForm();
				health2Form.initForm();
				health3Form.initForm();
				health4Form.initForm();
				health5Form.initForm();
				socialForm.initForm();
				
				enableTab(livingTab,enabled);
				enableTab(employmentTab,enabled);
				enableTab(crimeTab,enabled);
				enableTab(health1Tab,enabled);
				enableTab(health2Tab,enabled);
				enableTab(health3Tab,enabled);
				enableTab(health4Tab,enabled);
				enableTab(health5Tab,enabled);
				enableTab(socialTab,enabled);
			}
			
			protected function enableSectionK(enabled:Boolean):void
			{
				dischargeServices1Form.initForm();
				dischargeServices2Form.initForm();
				enableTab(dischargeServices1Tab,enabled);
				enableTab(dischargeServices2Tab,enabled);
			}
			
			protected function enableSectionA(enabled:Boolean):void
			{
				services1Form.initForm();
				services2Form.initForm();
				demoForm.initForm();
				demo2Form.initForm();
				military1Form.initForm();
				military2Form.initForm();
				
				enableTab(services1Tab,enabled);
				enableTab(services2Tab,enabled);
				enableTab(demoTab,enabled);
				enableTab(demo2Tab,enabled);
				enableTab(military1Tab,enabled);
				enableTab(military2Tab,enabled);
				
				if(enabled)
				{
					var dv:DateAgeValidator = recordForm.qInterviewDate.validator as DateAgeValidator;
					dv.max = 365;
				}

				demoForm.qDateOfBirth.validator.enabled = enabled;
			}
			
			protected function enableSectionI(enabled:Boolean):void
			{
				followupForm.initForm();
				enableTab(followupTab,enabled);
				if(enabled)
				{
					//Skip based on answer in intake DCI, since gender question not asked in followup
					if(global.activeClient.intake.data.source["Gender"] != 1)
						livingForm.qPregnant.enable();
					
					//The followup date cannot be before the intake date
					var dv:DateAgeValidator = recordForm.qInterviewDate.validator as DateAgeValidator;
					dv.max = (new Date().time - DateField.stringToDate(global.activeClient.intake.date,"MM/DD/YYYY").time) / 1000 / 60 / 60 / 24;
				}
			}
			
			protected function enableSectionJ(enabled:Boolean):void
			{
				dischargeForm.initForm();
				enableTab(dischargeTab,enabled);
				if(enabled)
				{
					//Skip based on answer in intake DCI, since gender question not asked in followup
					if(global.activeClient.intake.data.source["GenderCode"] != 1)
						livingForm.qPregnant.enable();
					
					//The followup date cannot be before the intake date
					var dv:DateAgeValidator = recordForm.qInterviewDate.validator as DateAgeValidator;
					dv.max = 1 + (new Date().time - DateField.stringToDate(global.activeClient.intake.date,"MM/DD/YYYY").time) / 1000 / 60 / 60 / 24;
				}
				dischargeForm.qDischargeDate.validator.enabled = enabled;
			}
			
			
			//Read GPRAVO, populate forms
			public function populateForm(gpra:ArrayCollection):void
			{
				var gpraVO:Array = gpra.source;
				var answer:String;
				var answerNum:Number;
				for each(var q:QuestionClass in questionDict)
				{
					if(["ServiceMemRelationship","ServiceMemExpOther","ServiceMemExpDeployed","ServiceMemExpInjured","ServiceMemExpCombatStress","ServiceMemExpDeceased"].indexOf(q.codeName) != -1)
						continue;
					answer = gpraVO[q.codeName];
					answerNum = parseInt(answer);
					if(q is QuestionTextRefusable)
					{
						var qr:QuestionTextRefusable = q as QuestionTextRefusable;
						if(answerNum == -1)
							q.disable(); //question is being skipped, put -1 for N/A, this may not be necessary
						else if(answerNum < -6)
							qr.refuseInput.selectedIndex = -6 - answerNum; //put -7 for Refused, -8 for DK, -9 for MI
						else
						{
							q.answer = answer;
							qr.refuseInput.selectedIndex = 0;
						}
						qr.refusedHandler(null);
					}
					else if(q is QuestionText)
					{
						if(answerNum == -1)
							q.disable(); //question is being skipped, put -1 for N/A
						else
							q.answer = answer;
					}
					else if(q is QuestionList)
					{
						var ql:QuestionList = q as QuestionList;
						if(ql.input.dataProvider.getItemAt(0) == "Yes" && ql.input.dataProvider.getItemAt(1) == "No")
						{
							if(answerNum == 1)
								q.answer = "Yes";
							else if(answerNum == 0)
								q.answer = "No";
						}
						/*else if(["MilitaryServed","ActiveDuty","FamilyActiveDuty","EducationYears","PhysicallyHurt"].indexOf(q.codeName) != -1)
						{
							ql.input.selectedIndex = answerNum; //These codes count up from 0
						}
						else if(q.codeName == "EmployStatus" && answerNum == 0)
						{
							q.answer = "Other";
						}*/
						else if(answerNum > 0)
							ql.input.selectedIndex = answerNum - 1;
						
						if(answerNum == -1)
							q.disable();
						else if(answerNum == -7)
							q.answer = "Refused";
						else if(answerNum == -8)
							q.answer = "Don't Know";
						else if(answerNum == -9)
							q.answer = "Missing Data";	
					}
					for each(var skip:SkipPattern in q.skipPatterns)
						skip.skipHandler(null);
				}
				
				for each(var q2:QuestionClass in questionDict)
				if(q2.validator != null)
					if(!q2.isSkipped && q2.inputControl.enabled)
						q2.validator.validate();
				
				military2Form.familyVeterans = new ArrayList();
				for(var i:Number = 1; i<=6; i++)
				{
					var num:Number = Number(gpraVO["ServiceMemRelationship"+String(i)]);
					if(num > 0)
					{
						var arr:Array = new Array();
						if(num == -9)
							arr.push("Missing Data");
						else
							arr.push(military2Form.qServiceMemRelationship.input.dataProvider.getItemAt(num -1));
						arr.push(gpraVO["ServiceMemExpOther"+String(i)]);
						
						num = gpraVO["ServiceMemExpDeployed"+String(i)];
						if(num == 1)
							arr.push("Yes");
						else if(num == 0)
							arr.push("No");
						else
							arr.push(military2Form.qServiceMemExpDeployed.input.dataProvider.getItemAt(-5 - num));
						
						num = gpraVO["ServiceMemExpInjured"+String(i)];
						if(num == 1)
							arr.push("Yes");
						else if(num == 0)
							arr.push("No");
						else
							arr.push(military2Form.qServiceMemExpInjured.input.dataProvider.getItemAt(-5 - num));
						
						num = gpraVO["ServiceMemExpCombatStress"+String(i)];
						if(num == 1)
							arr.push("Yes");
						else if(num == 0)
							arr.push("No");
						else
							arr.push(military2Form.qServiceMemExpCombatStress.input.dataProvider.getItemAt(-5 - num));
						
						num = gpraVO["ServiceMemExpDeceased"+String(i)];
						if(num == 1)
							arr.push("Yes");
						else if(num == 0)
							arr.push("No");
						else
							arr.push(military2Form.qServiceMemExpDeceased.input.dataProvider.getItemAt(-5 - num));
						
						military2Form.familyVeterans.addItem(arr);
					}
				}
				military2Form.updateFamilyValidator();
			}
			
			/**
			 * Save DCI
			 * 1. Validate for errors
			 * 2. Save each active form's data
			 * */
			protected function saveButton_clickHandler(event:MouseEvent):void
			{
				//Validate all active pages
				var results:Array = new Array();
				for each(var i:int in activeFormIndexes)
				{
					var tmpResults:Array = (dciForms[i] as DCIForm).validateForm();
					for(var j:int = 0; j < tmpResults.length; j++)
						results.push(tmpResults[j]);
				}
				
				if(results.length > 0)
				{
					Alert.show(results.join("\n"),"Validation Errors");
				}
				else
					parseForm();
			}
			
			//Parse Form Data, construct Assessment data
			protected function parseForm():void
			{
				var data:Array = new Array();
				
				for each(var i:int in activeFormIndexes)
				{
					data = (dciForms[i] as DCIForm).saveData(data);
				}
				
				//Add in other fields
				data["client_autoid"] = global.activeClient.autoid;
				
				//If doing discharge or followup, get gender from the intake
				if([InterviewType.INTAKE_GENERAL,InterviewType.INTAKE_SBIRT_BI,InterviewType.INTAKE_SBIRT_NEG,InterviewType.INTAKE_SBIRT_RT].indexOf(interviewType) == -1)
				{
					data["Gender"] = global.activeClient.intake.data.source["Gender"];
					data["IntakeDate"] = global.activeClient.intake.date;
				}
				
				//Create
				if(global.activeAssessment == null)
				{
					global.activeAssessment = new AssessmentVO();
					global.activeAssessment.episode_autoid = global.activeEpisode.autoid;
					global.activeAssessment.type = AssessType.DCI;
					global.activeAssessment.subtype = interviewType;
					global.activeAssessment.date = global.dateFormatter.format(new Date());
					global.activeAssessment.complete = 1;
					global.activeAssessment.data = data;
					createAssessmentResult.token = assessmentService.createAssessment(global.activeAssessment);
				}
				else //Update
				{
					//Preserve the autoids when saving
					var data_autoid:int = global.activeAssessment.data.source["autoid"];
					var assessment_autoid:int = global.activeAssessment.data.source["assessment_autoid"];
					data["autoid"] = data_autoid;
					data["assessment_autoid"] = assessment_autoid;
					global.activeAssessment.data = data;
					updateAssessmentDataResult.token = assessmentService.updateAssessmentData(global.activeAssessment);
				}
			}
			
			protected function createAssessmentResult_resultHandler(event:ResultEvent):void
			{
				Alert.show("DCI saved.");
				//Add in new autoid and put in assessments grid
				global.activeAssessment = createAssessmentResult.lastResult;
				global.pageClient.assessmentGroup.assessmentList.addItem(global.activeAssessment);
			}
			
			protected function updateAssessmentResult_resultHandler(event:ResultEvent):void
			{
				Alert.show("DCI updated.");
			}
			
			
			
			protected function headerButton_clickHandler(event:MouseEvent):void
			{
				var output:String = "";
				var file:FileReference = new FileReference();
				
				for each(var form:DCIForm in dciForms)
				{
					//get each question code from each form
					for each(var q:QuestionClass in form.questions)
						output += q.codeName + "," + q.className + "," + q.answerType.toString() + "\r\n";
				}
				file.save(output,"CodeNames.csv");
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<gpraservice:GpraService id="gpraService"
								 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
								 showBusyCursor="true"/>
		<assessmentservice:AssessmentService id="assessmentService"
											 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
											 showBusyCursor="true"/>
		<s:CallResponder id="createAssessmentResult" result="createAssessmentResult_resultHandler(event)"/>
		<s:CallResponder id="updateAssessmentDataResult" result="updateAssessmentResult_resultHandler(event)"/>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		
	</fx:Declarations>
	
	<mx:TabBar id="tabBar" x="0" y="0" dataProvider="viewStack"
			   direction="vertical"/>
	<mx:ViewStack id="viewStack" x="105" y="10" width="603" height="650"
				  backgroundColor="#EAEAEA" creationPolicy="all" dropShadowVisible="true">
		<s:NavigatorContent id="recordTab" width="100%" height="100%" label="Record">
			<dci:RecordForm id="recordForm"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="sbirtTab" width="100%" height="100%" label="SBIRT">
			<dci:SBIRTForm id="sbirtForm"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="services1Tab" width="100%" height="100%" label="Services1">
			<dci:Services1Form id="services1Form"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="services2Tab" width="100%" height="100%" label="Services2">
			<dci:Services2Form id="services2Form"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="demoTab" width="100%" height="100%" label="Demographic">
			<dci:DemographicsForm id="demoForm"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="demo2Tab" width="100%" height="100%" label="Demographic2">
			<dci:Demographics2Form id="demo2Form"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="military1Tab" width="100%" height="100%" label="Military1">
			<dci:Military1Form id="military1Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="military2Tab" width="100%" height="100%" label="Military2">
			<dci:Military2Form id="military2Form"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="drugTab" width="100%" height="100%" label="Drug1">
			<dci:DrugForm id="drugForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="drug2Tab" width="100%" height="100%" label="Drug2">
			<dci:Drug2Form id="drug2Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="livingTab" width="100%" height="100%" label="Living">
			<dci:LivingForm id="livingForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="employmentTab" width="100%" height="100%" label="Employment">
			<dci:EmploymentForm id="employmentForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="crimeTab" width="100%" height="100%" label="Crime">
			<dci:CrimeForm id="crimeForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="health1Tab" width="100%" height="100%" label="Health1">
			<dci:Health1Form id="health1Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="health2Tab" width="100%" height="100%" label="Health2">
			<dci:Health2Form id="health2Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="health3Tab" width="100%" height="100%" label="Health3">
			<dci:Health3Form id="health3Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="health4Tab" width="100%" height="100%" label="Health4">
			<dci:Health4Form id="health4Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="health5Tab" width="100%" height="100%" label="Health5">
			<dci:Health5Form id="health5Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="socialTab" width="100%" height="100%" label="Social">
			<dci:SocialForm id="socialForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="followupTab" width="100%" height="100%" label="Followup">
			<dci:FollowupForm id="followupForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="dischargeTab" width="100%" height="100%" label="Discharge">
			<dci:DischargeForm id="dischargeForm" />
		</s:NavigatorContent>
		<s:NavigatorContent id="dischargeServices1Tab" width="100%" height="100%" label="Services 1">
			<dci:Services1DischargeForm id="dischargeServices1Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="dischargeServices2Tab" width="100%" height="100%" label="Services 2">
			<dci:Services2DischargeForm id="dischargeServices2Form" />
		</s:NavigatorContent>
		<s:NavigatorContent id="saveTab" width="100%" height="100%" label="Save">
			<s:Button id="saveButton" x="200" y="200" label="Validate and Save GPRA"
					  click="saveButton_clickHandler(event)"/>
			<s:Button id="headerButton" x="200" y="300" label="Get Headers"
					  click="headerButton_clickHandler(event)"/>
		</s:NavigatorContent>
	</mx:ViewStack>
</s:NavigatorContent>
