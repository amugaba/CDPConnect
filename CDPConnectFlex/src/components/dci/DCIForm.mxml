<?xml version="1.0" encoding="utf-8"?>
<mx:Form xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%"
		 labelWidth="260">
	<fx:Script>
		<![CDATA[
			import components.pages.DciPage;
			import components.questions.QuestionClass;
			import components.questions.QuestionList;
			import components.questions.QuestionText;
			import components.questions.QuestionTextRefusable;
			import components.skips.SkipMultipleHandler;
			import components.skips.SkipPattern;
			import components.skips.SkipPatternMultiple;
			import components.skips.SkipPatternRange;
			import components.validators.DateAgeValidator;
			import components.validators.DropDownValidator;
			
			import mx.collections.ArrayList;
			import mx.controls.HRule;
			import mx.controls.Text;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.validators.NumberValidator;
			import mx.validators.Validator;
			
			import spark.events.IndexChangeEvent;
			
			protected var global:CDPConnectFlex = FlexGlobals.topLevelApplication as CDPConnectFlex;
			public var questions:Vector.<QuestionClass> = new Vector.<QuestionClass>();
			protected static var yn:ArrayList = new ArrayList([YES_VALUE,NO_VALUE]);
			protected static var ynrdm:ArrayList = new ArrayList([YES_VALUE,NO_VALUE,REFUSED_VALUE,DONT_KNOW_VALUE,MISSING_VALUE]);
			protected static var ynrm:ArrayList = new ArrayList([YES_VALUE,NO_VALUE,REFUSED_VALUE,MISSING_VALUE]);
			protected static var rdm:ArrayList = new ArrayList([REFUSED_VALUE,DONT_KNOW_VALUE,MISSING_VALUE]);
			
			public static var YES_CODE:int = 1;
			public static var NO_CODE:int = 0;
			public static var REFUSED_CODE:int = -7;
			public static var DONT_KNOW_CODE:int = -8;
			public static var MISSING_CODE:int = -9;
			
			public static var YES_VALUE:String = "Yes";
			public static var NO_VALUE:String = "No";
			public static var REFUSED_VALUE:String = "Refused";
			public static var DONT_KNOW_VALUE:String = "Don't Know";
			public static var MISSING_VALUE:String = "Missing Data";
			
			public var viewstackIndex:int = 0;
			protected var created:Boolean = false;
			
			public function addQuestion(q:QuestionClass, width:int = -1):void
			{
				questions.push(q);
				addElement(q);
				if(width != -1)
					q.inputControl.width = width;
			}
			
			public function initializeForm():void
			{
				if(!created)
					createForm();
				else
					reset();
			}
			
			public function createForm():void
			{
			}
			
			public function reset():void
			{
				for each(var q:QuestionClass in questions)
					q.restoreDefault();
			}
			
			protected function addDivider():void
			{
				var h:HRule = new HRule();
				h.height = 5;
				h.percentWidth = 100;
				addElement(h);
			}
			
			
			
			protected function addSkipDropDown(sourceQuestion:QuestionClass, skipAnswers:Array, skipQuestions:Array, inverse:Boolean = false):void
			{
				var skip:SkipPattern = new SkipPattern(sourceQuestion,skipQuestions,skipAnswers,[IndexChangeEvent.CHANGE],inverse);
				sourceQuestion.skipPatterns.push(skip);
			}
			
			protected function addSkipDropDownMultiple(handler:SkipMultipleHandler, sourceQuestion:QuestionClass, skipAnswers:Array):void
			{
				var skip:SkipPatternMultiple = new SkipPatternMultiple(handler,sourceQuestion,skipAnswers,[IndexChangeEvent.CHANGE]);
				sourceQuestion.skipPatterns.push(skip);
			}
			protected function addSkipTextNumberMultiple(handler:SkipMultipleHandler, sourceQuestion:QuestionClass, skipAnswers:Array):void
			{
				var skip:SkipPatternMultiple = new SkipPatternMultiple(handler,sourceQuestion,skipAnswers,[FlexEvent.VALUE_COMMIT,FocusEvent.MOUSE_FOCUS_CHANGE]);
				sourceQuestion.skipPatterns.push(skip);
			}
			protected function createSkipMultipleHandler(skipQuestions:Array):SkipMultipleHandler
			{
				return new SkipMultipleHandler(skipQuestions);
			}
			protected function addSkipTextNumber(sourceQuestion:QuestionClass, skipQuestions:Array, skipAnswers:Array):void
			{
				//(getQuestion(sourceQuestion) as QuestionTextNumber).addSkip(qArray,min,max,invert);
				var skip:SkipPattern = new SkipPattern(sourceQuestion,skipQuestions,skipAnswers,[FlexEvent.VALUE_COMMIT,FocusEvent.MOUSE_FOCUS_CHANGE]);
				sourceQuestion.skipPatterns.push(skip);
			}
			protected function addSkipTextRange(sourceQuestion:QuestionClass, skipQuestions:Array, min:int=0,max:int=0,invert:Boolean=false):void
			{
				//(getQuestion(sourceQuestion) as QuestionTextNumber).addSkip(qArray,min,max,invert);
				var skip:SkipPattern = new SkipPatternRange(sourceQuestion,skipQuestions,min,max,invert,[FlexEvent.VALUE_COMMIT,FocusEvent.MOUSE_FOCUS_CHANGE]);
				sourceQuestion.skipPatterns.push(skip);
			}
			
			protected function createText(text:String,style:String):void
			{
				var t:Text = new Text();
				t.text = text;
				t.width = 540-10;//This needs to be changed to access the GPRAPage viewstack.width value - 10
				t.styleName = style;
				addElement(t);
			}
			
			public function enableForm(enabled:Boolean):void
			{
			}
			
			public function saveData(gpraData:Array):void
			{
				for each(var q:QuestionClass in questions)
				{
					//gpraData[q.codeName] = q.answer;
					
					var valInt:int;
					var valString:String;
					
					if(q.isSkipped)
					{
						//gpraData[q.codeName] = null; //question is being skipped, put null for N/A
						continue;
					}
					else if(q is QuestionTextRefusable)
					{
						if(q.answerType == int)
							gpraData[q.codeName] = getDBValue_qTextRef_int(q as QuestionTextRefusable)
						else if(q.answerType == String)
							gpraData[q.codeName] = getDBValue_qTextRef_str(q as QuestionTextRefusable)
					}
					else if(q is QuestionText)
					{
						if(q.answerType == int && q.answer.length > 0)
							gpraData[q.codeName] = Number(q.answer);
						else if(q.answerType == String)
							gpraData[q.codeName] = q.answer;
					}
					else if(q is QuestionList)
					{
						gpraData[q.codeName] = getDBValue_qList(q as QuestionList);
					}
				}
			}
			
			private function getDBValue_qTextRef_int(q:QuestionTextRefusable):int
			{				
				if(q.refuseInput.selectedItem == REFUSED_VALUE)
					return REFUSED_CODE;
				else if(q.refuseInput.selectedItem == DONT_KNOW_VALUE)
					return DONT_KNOW_CODE;
				else if(q.refuseInput.selectedItem == MISSING_VALUE)
					return MISSING_CODE;
				else
					return Number(q.answer);
			}
			
			private function getDBValue_qTextRef_str(q:QuestionTextRefusable):String
			{				
				if(q.refuseInput.selectedItem == REFUSED_VALUE)
					return REFUSED_CODE.toString();
				else if(q.refuseInput.selectedItem == DONT_KNOW_VALUE)
					return DONT_KNOW_CODE.toString();
				else if(q.refuseInput.selectedItem == MISSING_VALUE)
					return MISSING_CODE.toString();
				else
					return q.answer;
			}
			
			private function getDBValue_qList(q:QuestionList):int
			{
				if(q.answer == YES_VALUE)
					return YES_CODE;
				else if(q.answer == NO_VALUE)
					return NO_CODE;
				else if(q.answer == REFUSED_VALUE)
					return REFUSED_CODE;
				else if(q.answer == DONT_KNOW_VALUE)
					return DONT_KNOW_CODE;
				else if(q.answer == MISSING_VALUE)
					return MISSING_CODE;
				else
					return q.getAnswerIndex();
			}
			
			private function loadDBValue_qRef_int(q:QuestionTextRefusable, code:int):void
			{
				if(code == REFUSED_CODE)
					q.refuseInput.selectedIndex = 1;
				else if(code == DONT_KNOW_CODE)
					q.refuseInput.selectedIndex = 2;
				else if(code == MISSING_CODE)
					q.refuseInput.selectedIndex = 3;
				else
					q.answer = code.toString();
			}
			
			private function loadDBValue_qRef_str(q:QuestionTextRefusable, val:String):void
			{
				var code:int = parseInt(val);
				if(code == REFUSED_CODE)
					q.refuseInput.selectedIndex = 1;
				else if(code == DONT_KNOW_CODE)
					q.refuseInput.selectedIndex = 2;
				else if(code == MISSING_CODE)
					q.refuseInput.selectedIndex = 3;
				else
					q.answer = val;
			}
			
			private function loadDBValue_qList(q:QuestionList, code:int):void
			{
				if(isNaN(code))
					q.disable();
				else
				{
					//first set it the index naively
					q.setAnswerIndex(code);
					//then if it can accept a special code, set it
					if(code == REFUSED_CODE)
						q.answer = REFUSED_VALUE;
					else if(code == DONT_KNOW_CODE)
						q.answer = DONT_KNOW_VALUE;
					else if(code == MISSING_CODE)
						q.answer = MISSING_VALUE;
					else if(code == YES_CODE)
						q.answer = YES_VALUE;
					else if(code == NO_CODE)
						q.answer = NO_VALUE;
				}
			}
			
			protected function questionValue(answer:String, q:QuestionList):int
			{
				if(answer == "Yes")
					return 1;
				if(answer == "No")
					return 0;
				if(answer == "Refused")
					return -7;
				if(answer == "Don't Know")
					return -8;
				if(answer == "Missing Data")
					return -9;
				return q.input.dataProvider.getItemIndex(answer);
			}			
			
		]]>
	</fx:Script>
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		.h1{
			fontWeight: bold;
		}
	</fx:Style>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
</mx:Form>
