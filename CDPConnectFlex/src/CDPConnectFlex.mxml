<?xml version="1.0" encoding="utf-8"?>
<!--
CDP Connect
DCI Upload System
By David Tidd

-->
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:custom="components.*"
			   xmlns:gpraservice="services.gpraservice.*"
			   xmlns:styles="com.adobe.fiber.styles.*"
			   xmlns:clientservice="services.clientservice.*"
			   xmlns:userservice="services.userservice.*"
			   xmlns:miscservice="services.miscservice.*"
			   width="715" height="800" minWidth="955" minHeight="620"
			   backgroundColor="#E2E2E8" currentState="Login"
			   pageTitle="CDP Connect" xmlns:pages="components.pages.*">
	<fx:Script>
		<![CDATA[
			import components.assessment.AssessType;
			import components.specialrules.RulesManager;
			import components.specialrules.SbirtRules;
			
			import flash.globalization.DateTimeFormatter;
			import flash.globalization.LocaleID;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.buttonBarClasses.ButtonBarButton;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.rpc.events.ResultEvent;
			
			import valueObjects.AssessmentVO;
			import valueObjects.ClientVO;
			import valueObjects.EpisodeVO;
			import valueObjects.GrantVO;
			import valueObjects.UserVO;			

			[Bindable] public var activeClient:ClientVO = null;
			[Bindable] public var activeUser:UserVO = null;
			[Bindable] public var activeEpisode:EpisodeVO = null;
			[Bindable] public var activeGrant:GrantVO = null;
			[Bindable] public var activeAssessment:AssessmentVO = null;
			
			public var pageHomeButton:ButtonBarButton;
			public var pageClientButton:ButtonBarButton;
			public var pageAssessButton:ButtonBarButton;
			public var pageDCIButton:ButtonBarButton;
			public var pageAdminButton:ButtonBarButton;
			
			public var userList:ArrayList = new ArrayList();
			[Bindable] public var staffList:ArrayList = new ArrayList();
			[Bindable] public var facilityList:ArrayList = new ArrayList(); //maybe BIndable
			
			public var dateFormatter:DateTimeFormatter = new DateTimeFormatter(LocaleID.DEFAULT);
			public var adminMode:Boolean = false;
			private var creationComplete:Boolean = false;
			public static var DEBUG_MODE:int = 2;//0 = release, 1 = remote debug, 2 = local debug
			public var preparationStepsLeft:int = 3;
			
			//Project type switches
			public var rulesManager:RulesManager;
			
			protected function loginCompleteHandler(event:FlexEvent):void
			{
				//for subsequent logins, do setup upon entering the state immediately
				if(creationComplete)
					loginHelper();
			}
			
			protected function pageViewstack_creationCompleteHandler(event:FlexEvent):void
			{
				//The first time login is done during the application, do setup after creation
				loginHelper();
			}
			
			protected function loginHelper():void
			{
				//Get staff list, facilities list, and grant details
				getUsers.token = userService.getUsers();
				getFacilities.token = miscService.getFacilities();
				getGrantResult.token = miscService.getGrant(activeUser.grantid);
				
				//Set up button bar
				pageHomeButton = pageButtonBar.getChildAt(0) as ButtonBarButton;
				pageClientButton = pageButtonBar.getChildAt(1) as ButtonBarButton;
				pageAssessButton = pageButtonBar.getChildAt(2) as ButtonBarButton;
				pageDCIButton = pageButtonBar.getChildAt(3) as ButtonBarButton;
				pageAdminButton = pageButtonBar.getChildAt(4) as ButtonBarButton;
				enableLeftButtonsOnly(pageHomeButton);
				pageAdminButton.visible = adminMode;
				
				//Misc
				dateFormatter.setDateTimePattern("MM/dd/yyyy");
				creationComplete = true;
				rulesManager = new RulesManager(FlexGlobals.topLevelApplication as CDPConnectFlex)
			}
			
			protected function headerClientText(activeClient:ClientVO):String
			{
				var s:String = "";
				if(activeClient != null)
					s = "ID: "+activeClient.clientid+"   Name: "+activeClient.lastname+", "+activeClient.firstname;
				return s;
			}
			protected function headerEpisodeText(episode:EpisodeVO):String
			{
				var s:String = "";
				if(episode != null)
					s = "#" + episode.number + "   Facility: " + episode.facility + "   Staff: " + episode.staff;
				return s;
			}
			protected function getFacilities_resultHandler(event:ResultEvent):void
			{
				for each(var f:String in getFacilities.lastResult)
					facilityList.addItem(f);
				attemptHomePage();
			}
			
			protected function getUsers_resultHandler(event:ResultEvent):void
			{
				//initialize BHS name and intials arrays
				for each(var user:UserVO in getUsers.lastResult)
				{
					userList.addItem(user.name);
					staffList.addItem(user.name);
				}
					
				attemptHomePage();				
			}
			
			protected function attemptHomePage():void
			{
				preparationStepsLeft--;
				if(preparationStepsLeft == 0)
				{
					pageHome.creationCompleteHandler();
					pageViewstack.selectedChild = pageHome;
				}
			}

			protected function logoutButton_clickHandler(event:MouseEvent):void
			{
				activeUser = null;
				activeClient = null;
				activeAssessment = null;
				pageHomeButton.enabled = false;
				pageClientButton.enabled = false;
				pageAssessButton.enabled = false;
				pageDCIButton.enabled = false;
				pageHome.resetPage();
				pageLogin.resetPage();
				currentState = "Login";
			}
			
			protected function getGrantResult_resultHandler(event:ResultEvent):void
			{
				activeGrant = getGrantResult.lastResult;
				rulesManager.sbirtEnabled = activeGrant.sbirt == 1;
				pageAdmin.populateSettings();
				attemptHomePage();
			}
			
			//Disable all buttons except the currently selected one
			public function disableAllOtherButtons(b:ButtonBarButton):void
			{
				b.enabled = true;
				for(var i:int = 0; i < pageButtonBar.getChildren().length; i++)
				{
					if(b != pageButtonBar.getChildAt(i))
						(pageButtonBar.getChildAt(i) as ButtonBarButton).enabled = false;
				}
			}
			
			//Enable all buttons to the left (higher in hierarchy) of the current one
			public function enableLeftButtonsOnly(b:ButtonBarButton):void
			{
				if(b == pageHomeButton)
				{
					pageHomeButton.enabled = true;
					pageAdminButton.enabled = true;
					pageClientButton.enabled = false;
					pageAssessButton.enabled = false;
					pageDCIButton.enabled = false;
				}
				else if(b == pageClientButton)
				{
					pageHomeButton.enabled = true;
					pageAdminButton.enabled = true;
					pageClientButton.enabled = true;
					pageAssessButton.enabled = false;
					pageDCIButton.enabled = false;
				}
				else if(b == pageAssessButton)
				{
					pageHomeButton.enabled = true;
					pageClientButton.enabled = true;
					pageAdminButton.enabled = true;
					pageAssessButton.enabled = true;
					pageDCIButton.enabled = false;
				}
				else if(b == pageDCIButton)
				{
					pageHomeButton.enabled = true;
					pageClientButton.enabled = true;
					pageAdminButton.enabled = true;
					pageAssessButton.enabled = false;
					pageDCIButton.enabled = true;
				}
			}
			
		]]>
	</fx:Script>
	<s:states>
		<s:State name="State1" enterState="loginCompleteHandler(event)"/>
		<s:State name="Login"/>
	</s:states>
	<fx:Declarations>
		<clientservice:ClientService id="clientService"
									 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
									 showBusyCursor="true"/>
		<miscservice:MiscService id="miscService"
								 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
								 showBusyCursor="true"/>
		<s:CallResponder id="getFacilities" result="getFacilities_resultHandler(event)"/>
		<s:CallResponder id="getUsers" result="getUsers_resultHandler(event)"/>
		<userservice:UserService id="userService"
								 fault="Alert.show(event.fault.faultString + '\n' + event.fault.faultDetail)"
								 showBusyCursor="true"/>
		<s:CallResponder id="getGrantResult" result="getGrantResult_resultHandler(event)"/>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		
	</fx:Declarations>
	<mx:ViewStack id="pageViewstack" x="0" y="100" width="100%" height="100%"  includeIn="State1"
				  backgroundColor="#E2E2E8" chromeColor="#6376A5" color="#000000"
				  contentBackgroundColor="#FFFFFF" creationPolicy="all" creationComplete="pageViewstack_creationCompleteHandler(event)">
		<pages:HomePage id="pageHome" width="100%" height="100%" label="Home" />
		<pages:ClientPage id="pageClient" width="100%" height="100%" label="Client" />
		<pages:AssessmentPage id="pageAssessment" width="100%" height="100%" label="Screen"/>
		<pages:DciPage id="pageDCI" width="100%" height="100%" label="DCI"/>
		<pages:AdminPage id="pageAdmin" width="100%" height="100%" label="Admin"/>
	</mx:ViewStack>
	<s:Image x="0" y="0" source="assets/ConnectBanner.png"/>
	<s:Group includeIn="State1" x="10" y="3" width="695" height="90" >
		<s:layout>
			<s:BasicLayout/>
		</s:layout>
		<s:FormItem x="150" y="27" label="Open Client:">
			<s:Label id="headerClientLabel" text="{headerClientText(activeClient)}"/>
		</s:FormItem>
		<s:FormItem x="150" y="46" label="Open Episode:">
			<s:Label id="headerEpisodeLabel" text="{headerEpisodeText(activeEpisode)}"/>
		</s:FormItem>
		<s:Button id="logoutButton" x="606" y="5" label="Logout"
				  click="logoutButton_clickHandler(event)"/>
	</s:Group>
	<mx:ToggleButtonBar id="pageButtonBar" x="171" y="10" dataProvider="{pageViewstack}" includeIn="State1"/>
	
	<pages:LoginPage id="pageLogin" width="100%" height="100%" label="Login" includeIn="Login"/>
</s:Application>
